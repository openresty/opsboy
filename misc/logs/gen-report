#!/usr/bin/env perl

use strict;
use warnings;

use JSON::XS;
use Template;
use File::Copy "cp";

my $dir = shift
    or die "No input directory specified.\n";

my @jsonfiles = glob "$dir/*.json";
if (!@jsonfiles) {
    die "No JSON files found under $dir/.\n";
}

my %github_links = (
    'lua-resty-upstream-healthcheck' => ['openresty', 'lua-resty-upstream-healthcheck'],
    'lua-resty-core' => ['openresty', 'lua-resty-core'],
    'lua-resty-logger-socket' => ['cloudflare', 'lua-resty-logger-socket'],
    'lua-resty-lock' => ['openresty', 'lua-resty-lock'],
    'lua-resty-websocket' => ['openresty', 'lua-resty-websocket'],
    'lua-resty-dns' => ['openresty', 'lua-resty-dns'],
    'lua-resty-upload' => ['openresty', 'lua-resty-upload'],
    'lua-resty-string' => ['openresty', 'lua-resty-string'],
    'lua-resty-mysql' => ['openresty', 'lua-resty-mysql'],
    'lua-resty-redis' => ['openresty', 'lua-resty-redis'],
    'lua-resty-memcached' => ['openresty', 'lua-resty-memcached'],
    'lua-resty-lrucache' => ['openresty', 'lua-resty-lrucache'],
    'lua-resty-limit-traffic' => ['openresty', 'lua-resty-limit-traffic'],
    'lua-resty-shell' => ['openresty', 'lua-resty-shell'],
    'lua-resty-signal' => ['openresty', 'lua-resty-signal'],
    #'lua-tablepool' => ['openresty', 'lua-tablepool'],
    ngx_iconv => ['calio', 'iconv-nginx-module'],
    ngx_form_input => ['calio', 'form-input-nginx-module'],
    ngx_coolkit => ['FRiCKLE', 'ngx_coolkit'],
    ngx_encrypted_session => ['openresty', 'encrypted-session-nginx-module'],
    ngx_array_var => ['openresty', 'array-var-nginx-module'],
    ngx_drizzle => ['openresty', 'drizzle-nginx-module'],
    ngx_echo => ['openresty', 'echo-nginx-module'],
    ngx_eval => ['openresty', 'nginx-eval-module'],
    ngx_headers_more => ['openresty', 'headers-more-nginx-module'],
    ngx_lua => ['openresty', 'lua-nginx-module'],
    ngx_memc => ['openresty', 'memc-nginx-module'],
    ngx_postgres => ['openresty', 'ngx_postgres'],
    ngx_rds_csv => ['openresty', 'rds-csv-nginx-module'],
    ngx_rds_json => ['openresty', 'rds-json-nginx-module'],
    ngx_redis2 => ['openresty', 'redis2-nginx-module'],
    ngx_replace_filter => ['openresty', 'replace-filter-nginx-module'],
    ngx_set_misc => ['openresty', 'set-misc-nginx-module'],
    ngx_srcache => ['openresty', 'srcache-nginx-module'],
    ngx_xss => ['openresty', 'xss-nginx-module'],
    ngx_example_lua => ['bakins', 'nginx-example-lua-module'],
    ngx_lua_upstream => ['openresty', 'lua-upstream-nginx-module'],
    ngx_stream_echo => ['openresty', 'stream-echo-nginx-module'],
    ngx_stream_lua => ['openresty', 'stream-lua-nginx-module'],
    sregex => ['openresty', 'sregex'],
    'resty-cli' => ['openresty', 'resty-cli'],
);

my $last_test_date;
if (open my $in, "LAST_TIME") {
    $last_test_date = <$in>;
    close $in;
}

my $data = {
};

my $jsonxs = JSON::XS->new->ascii;

my $new_test_date;

for my $jsonfile (@jsonfiles) {
    open my $in, $jsonfile
        or die "Cannot open $jsonfile for reading: $!\n";
    my $json = do { local $/; <$in> };
    close $in;

    my $ast = $jsonxs->decode($json);

    my $arch = $ast->{arch};
    my $arch_group;
    if (!defined $data->{$arch}) {
        $arch_group = {};
        $data->{$arch} = $arch_group;

    } else {
        $arch_group = $data->{$arch};
    }

    my $test_date = $ast->{date};

    if (!defined $new_test_date || $new_test_date lt $test_date) {
        $new_test_date = $test_date;
    }

    my $is_new;
    if (defined $last_test_date && $test_date gt $last_test_date) {
        $is_new = 1;
    }

    my $recs = $ast->{records};
    for my $rec (@$recs) {
        my $nginx_version = $rec->{nginx_version};
        if (!defined $nginx_version) {
            $nginx_version = "miscellaneous";
        }

        if ($nginx_version =~ /\b1\.0\.15\b/
            || $nginx_version =~ /\b1\.2\.[134678]\b/
            || $nginx_version =~ /\b1\.3\.(?:11|[467])\b/
            || $nginx_version =~ /\b1\.4\.[1-3]\b/
            || $nginx_version =~ /\b1\.5\.(?:[89]|1[12])\b/
            || $nginx_version =~ /\b1\.7\.(?:10|[0247])\b/
            || $nginx_version =~ /\b1\.9\.[237]\b/
            || $nginx_version =~ /\b1\.11\.\d+\b/
            || $nginx_version =~ /\b1\.15\.6\b/)
        {
            next;
        }

        my $nginx_group;
        if (!defined $arch_group->{$nginx_version}) {
            $nginx_group = {};
            $arch_group->{$nginx_version} = $nginx_group;
        } else {
            $nginx_group = $arch_group->{$nginx_version};
        }

        my $module = $rec->{module};

        #warn $module;

        if ($module eq 'ngx_stream_echo' || $module eq 'lua-tablepool') {
            next;
        }

        my $module_group;
        if (!defined $nginx_group->{$module}) {
            $module_group = {};
            $nginx_group->{$module} = $module_group;
        } else {
            $module_group = $nginx_group->{$module};
        }

        my $mode = $rec->{mode};
        my $mode_group;
        if (!defined $module_group->{$mode}) {
            $mode_group = {};
            $module_group->{$mode} = $mode_group;
        } else {
            $mode_group = $module_group->{$mode};
        }

        if (defined $mode_group->{test_date}
            && defined $mode_group->{commit_date})
        {
            if ($test_date lt $mode_group->{test_date}) {
                warn "Skip $arch > $nginx_version > $module > $mode ($test_date).\n";
                next;
            }

            #warn "Overriding $arch > $nginx_version > $module > $mode ($test_date later than $mode_group->{test_date})\n";
        }

        $mode_group->{test_date} = $test_date;
        $mode_group->{commit_date} = $rec->{commit_date};
        $mode_group->{result} = $rec->{result};
        $mode_group->{expected_fails} = $rec->{expected_fails};
        $mode_group->{true_fails} = $rec->{true_fails};
        $mode_group->{tests} = $rec->{tests};
        $mode_group->{files} = $rec->{files};
        $mode_group->{commit} = substr($rec->{commit} // '', 0, 8);
        $mode_group->{test_date} = $test_date;
        $mode_group->{elapsed} = $rec->{secs};
        $mode_group->{warns} = $rec->{warns};
        $mode_group->{config_error} = $rec->{config_error};
        $mode_group->{comp_errors} = $rec->{comp_errors};
        $mode_group->{is_new} = $is_new;
        $mode_group->{luajit_ver} = $rec->{luajit_ver};
        $mode_group->{luajit_rev} = $rec->{luajit_rev};
        $mode_group->{openssl_ver} = $rec->{openssl_ver};
        $mode_group->{segfaults} = $rec->{segfaults};
    }
}

my $outfile = "ast.json";
open my $out, ">$outfile"
    or die "Cannot open $outfile for writing: $!\n";
print $out $jsonxs->pretty->encode($data);
close $out;

mkdir 'html/' if ! -d 'html/';

my $tt2 = Template->new;

for my $arch_name (sort keys %$data) {
    my $arch = $data->{$arch_name};
    (my $arch_anchor = $arch_name) =~ s/[^-\w]/_/g;
    for my $ngx_ver (sort keys %$arch) {
        my $ngx = $arch->{$ngx_ver};
        (my $ngx_anchor = $ngx_ver) =~ s/[^-\w]/_/g;
        $ngx_anchor = $arch_anchor . '_' . $ngx_anchor;

        for my $module_name (sort keys %$ngx) {
            my $module = $ngx->{$module_name};

            for my $mode_name (sort keys %$module) {
                my $mode = $module->{$mode_name};

                my $link = $ngx_anchor . '_' . $module_name . '_'
                    . $mode_name . '.html';

                $mode->{link} = $link;

                my $outfile = "html/$link";
                $mode->{failures} ||= [];
                $mode->{true_fails} ||= [];

                my $github_link = $github_links{$module_name};
                if ($github_link) {
                    #warn "Found github link for $module_name\n";
                    $github_link = "github.com/$github_link->[0]/$github_link->[1]";

                } else {
                    #warn "NOT Found github link for $module_name\n";
                }

                $tt2->process('detailed.tt', {
                    name => $module_name,
                    arch => $arch_name,
                    mode => $mode_name,
                    rec => $mode,
                    github_link => $github_link,
                }, $outfile) or die $tt2->error();

                #warn "wrote $outfile\n";
            }
        }

        $ngx->{anchor} = $ngx_anchor;
    }
    #warn $arch_name;
    $arch->{anchor} = $arch_anchor;
}

$outfile = "html/index.html";

$tt2->process('index.tt', { ast => $data }, 'html/index.html')
    or die $tt2->error();

warn "wrote $outfile\n";

warn "New test date: $new_test_date\n";

if (!defined $last_test_date || $new_test_date ne $last_test_date) {
    if (defined $last_test_date) {
        cp("LAST_TIME", "LAST_TIME.old") or
            die "Cannot cp LAST_TIME to LAST_TIME.old\n";
    }

    open my $out, ">LAST_TIME" or
        die "Cannot write to LAST_TIME: $!\n";
    print $out $new_test_date;
    close $out;
}

